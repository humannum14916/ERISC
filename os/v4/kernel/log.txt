
namespace Log {
  
function stackless void logStr(array[char] str){
  for(define int i=0;str[i] != null;i=i+1){
    call logChar(str[i]);
  };
};

function stackless void printf(
  array[char] msg, array[int] reps
) {
  define int ri = 0;
  for(define int i=0;msg[i]!=null;i=i+1){
    if(msg[i] == "%"){
      call logInt(reps[ri]);
      ri = ri + 1;
    } else {
      call logChar(msg[i]);
    };
  };
};

function stackless void logChar(char c){
  asm "TRS Log.logChar.c,TTY";
};

function stackless void logInt(int i){
  //get highest digit
  define int maxL = 1;
  define int max = 1;
  while(maxL < i | maxL == i){
    max = maxL;
    maxL = Math.mult(maxL,10);
  };
  //convert
  define bool working = true;
  while(working){
    define int curD = 0;
    while(!(i < max)){
      curD = curD + 1;
      i = i - max;
    };
    call Log.logChar({char}(curD + 48));
    if(max == 1){
      working = false;
    };
    max = Math.div(max,10);
  };
};

};
